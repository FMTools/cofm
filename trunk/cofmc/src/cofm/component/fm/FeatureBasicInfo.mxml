<?xml version="1.0" encoding="utf-8"?>
<s:SkinnableContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
					  xmlns:s="library://ns.adobe.com/flex/spark" 
					  xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:fm="cofm.component.fm.*">
	<s:layout>
		<s:VerticalLayout/>
	</s:layout>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import cofm.event.*;
			import cofm.model.*;
			import cofm.util.*;
			
			import spark.components.HGroup;
			import spark.components.TextArea;
			
			public function updateBasicInfo(): void {
				infoBox.removeAllElements();
				
				for each (var obj: Object in CurrentFeature.instance().basicInfo) {
					
					var wrapper: HGroup = new HGroup();
					
					// 1. Name of the attribute
					var attrName: Label = new Label();
					attrName.text = obj.@label + (obj.@unit != undefined ? " (" + obj.@unit + ")" : "")
									+ ": ";
					wrapper.addElement(attrName);
					
					// 2. (Primary) value of the attribute
					if (String(obj.@type) == Cst.ATTR_TYPE_TEXT) {
						var txtValue: TextArea = new TextArea();
						txtValue.width = 300;
						txtValue.height = 100;
						txtValue.editable = false;
						txtValue.text = obj.@value;
						wrapper.addElement(txtValue);
						
						addRate(obj, wrapper);
					} else if (String(obj.@type) == Cst.ATTR_TYPE_ENUM) {
						for each (var e: Object in obj.enum) {
							addStrValue(e, wrapper);
						}
					} else {
						addStrValue(obj, wrapper);
					}
					
					infoBox.addElement(wrapper);
				}
			}
			
			private function addRate(obj: Object, wrapper: HGroup): void {
				if (obj.@rate != undefined) {
					var rate: Label = new Label();
					rate.text = "(" + obj.@rate + "%)";
					wrapper.addElement(rate);
				}
			}
			
			private function addStrValue(obj: Object, wrapper: HGroup): void {
				var strValue: Label = new Label();
				strValue.text = obj.@value;
				wrapper.addElement(strValue);
				
				addRate(obj, wrapper);
			}
			
		]]>
	</fx:Script>
	
	<s:Label text="{RS.m_fe_basic_votes}"/>
	<fm:FeatureVoteChart width="100%" height="100"/>
	
	<s:VGroup id="infoBox" />
</s:SkinnableContainer>
