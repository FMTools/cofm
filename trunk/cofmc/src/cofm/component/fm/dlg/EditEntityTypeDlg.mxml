<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" width="660" height="580"
			   title="Add or change type definition"
			   xmlns:common="cofm.component.common.*"
			   close="onCancel();"
			   creationComplete="initView();">
	<s:layout>
		<s:VerticalLayout/>
	</s:layout>
	<s:states>
		<s:State name="EntityBegin" />
		<s:State name="TypeSelected" />
		<s:State name="AttrSelected" />
	</s:states>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import cofm.command.*;
			import cofm.component.common.PromptWindow;
			import cofm.event.*;
			import cofm.model.*;
			import cofm.util.*;
			
			import mx.collections.XMLListCollection;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.managers.PopUpManager;
			
			private static const TO_BE_DELETED: String = "____TO_BE_DELETED____";
			private var typeId: int = -2;  // starts below -1
			
			private var selectedTypeId: String;
			private var selectedTypeName: String;
			
			private static const nameAttrXml: XML = <attrDef name={Cst.ATTR_FEATURE_NAME} type={Cst.ATTR_TYPE_STRING} multi="true" dup="false"/>;
			private static const desAttrXml: XML = <attrDef name={Cst.ATTR_FEATURE_DES} type={Cst.ATTR_TYPE_TEXT} multi="true" dup="true"/>;
			
			private var promptDlg: PromptWindow = null;
			
			[Bindable]
			public var entityTypes: XMLListCollection;
			
			[Bindable]
			public var typeTreeData: XMLListCollection;
			
			private function initView(): void {
				// Copy entity types from Model and generate into a tree.
				entityTypes = new XMLListCollection();
				typeTreeData = new XMLListCollection(new XMLList(<entype/>));
				for each (var t: Object in Model.instance().entypes.source) {
					entityTypes.addItem(XML(t).copy());
					
					var rootType: XML = Model.instance().getRootType(XML(t));
					if (getNodeById(rootType.@id) == null) {
						var base: XML = rootNode();
						for each (var curType: Object in Model.instance().getEntityTypeHierarchy(rootType.@id)) {
							var cur: XML = XML(curType).copy();
							// Remove the <attrDefs/> part from the <entype> node, otherwise
							// the <attrDefs/> will appear as a node in the tree.
							delete cur.attrDefs;
							base.appendChild(cur);
							base = cur;
						}
					}
				}
				
				ClientEvtDispatcher.instance().addEventListener(
					OperationCommitEvent.EXECUTED_ON_LOCAL, onOperationExecuted);
			}
			
			private function rootNode(): XML {
				return XML(typeTreeData.getItemAt(0));
			}
			
			private function getNodeById(id: String): XML {
				var nodes: XMLList = rootNode()..entype.(@id==id);
				if (nodes.length() > 0) {
					return XML(nodes[0]);
				}
				return null;
			}
			
			private function onOperationExecuted(evt: OperationCommitEvent): void {
				// Update selectedTypeId, entityTypes, typeTreeData, attrId
				// Create default attrs if new type has been created (use evt.model.handle to locate
				// the original ID of the entity type).
				// Handles changing on type only.
				if (evt.response == null) {
					return;
				}
				
				if (evt.response[Cst.FIELD_RSP_SOURCE_NAME] == Cst.REQ_EA_ENTITY_TYPE) {
					// Handle adding or editing entity type
					var handle: int = int(evt.response[Cst.FIELD_CMD_HANDLE]);
					var typeXMLs: XMLList;
					var treeXML: XML;
					if (handle < -1) {
						// An adding operation, get type XML by HANDLE, and set real type ID
						// and add default attributes
						typeXMLs = entityTypes.source.(@id==handle);
						if (typeXMLs.length() > 0) {
							typeXMLs[0].@id = evt.response["typeId"];
						}
						treeXML = getNodeById(String(handle));
						if (treeXML != null) {
							treeXML.@id = evt.response["typeId"];
						}
						new AddAttributeCommand(Cst.ATTR_FEATURE_NAME,
							Cst.ATTR_TYPE_STRING, int(evt.response["typeId"]),
							true, false, ModelCollection.instance().currentModelId).execute();
						new AddAttributeCommand(Cst.ATTR_FEATURE_DES,
							Cst.ATTR_TYPE_TEXT, int(evt.response["typeId"]),
							true, true, ModelCollection.instance().currentModelId).execute();
					} else {
						// An editing operation, get type XML by ID, and set new name
						typeXMLs = entityTypes.source.(@id==String(evt.response["typeId"]));
						if (typeXMLs.length() > 0) {
							typeXMLs[0].@name = evt.response["typeName"];
						}
						treeXML = getNodeById(String(evt.response["typeId"]));
						if (treeXML != null) {
							treeXML.@name = evt.response["typeName"];
						}
					}
					
					if (promptDlg != null) {
						PopUpManager.removePopUp(promptDlg);
					}
				}
			}
			
			private function onTypeNameChanged(evt: ListEvent): void {
				var curType: XML = XML(rootNode()..entype.(@id==selectedTypeId)[0]);
				var newName: String = String(curType.@name);
				if (newName != selectedTypeName) {
					new EditAddEntityTypeCommand(newName,
												-1,
												ModelCollection.instance().currentModelId,
												int(curType.@id)).execute();
				}
			}
			
			private function onTypeClicked(evt: ListEvent): void {
				this.currentState = "TypeSelected";	
				
				selectedTypeId = String(typeTree.selectedItem.@id);
				selectedTypeName = String(typeTree.selectedItem.@name);
				
				attrGrid.dataProvider = 
					entityTypes.source.(@id==selectedTypeId)[0].attrDefs.attrDef;
				
				attrForm.reset();
				attrForm.submitBtnLabel = "Add Attribute";
			}
			
			private function onNewType(): void {
				// Auto-generated temporary ID is equal or less than 0.
				var n: XML = <entype id={typeId--} name="Enter type name here"/>;
				rootNode().appendChild(n);
				typeTree.selectedItem = n;
				selectedTypeId = String(n.@id);
				selectedTypeName = String(n.@name);
				
				// Add the default attributes and copy to entityTypes.
				var c: XML = n.copy();
				c.appendChild(<attrDefs/>);
				c.attrDefs.appendChild(nameAttrXml.copy());
				c.attrDefs.appendChild(desAttrXml.copy());
				entityTypes.addItem(c);
				
				promptDlg = PromptWindow(PopUpManager.createPopUp(this, PromptWindow, true));
				promptDlg.width = 200;
				promptDlg.height = 90;
				promptDlg.prompt = "Waiting...";
				PopUpManager.centerPopUp(promptDlg);
				
				// Add a random number to avoid name conflict at the server.
				new EditAddEntityTypeCommand(String(n.@name) + Math.random().toString(),
					-1,
					ModelCollection.instance().currentModelId,
					-1,
					int(n.@id)).execute();
			}
			
			private function onNewSubtype(): void {
				var parentType: XML = XML(entityTypes.source.(@id==selectedTypeId)[0]);
				var parentNode: XML = getNodeById(selectedTypeId);
				if (parentNode == null) {
					return;
				}
				
				var n: XML = <entype id={typeId--} superId={parentType.@id} name="Enter type name here"/>;
				parentNode.appendChild(n);
				typeTree.expandItem(parentNode, true);
				typeTree.selectedItem = n;
				selectedTypeId = String(n.@id);
				selectedTypeName = String(n.@name);
				
				// A subtype inherits all attributes from its parent.
				var type: XML = n.copy();
				type.attrDefs = parentType.attrDefs.copy();
				entityTypes.addItem(type);		
				
				promptDlg = PromptWindow(PopUpManager.createPopUp(this, PromptWindow, true));
				promptDlg.width = 200;
				promptDlg.height = 90;
				promptDlg.prompt = "Waiting...";
				PopUpManager.centerPopUp(promptDlg);
				
				new EditAddEntityTypeCommand(String(n.@name) + Math.random().toString(),
					int(n.@superId),
					ModelCollection.instance().currentModelId,
					-1,
					int(n.@id)).execute();
			}
			
			private function onAttributeClicked(): void {
				this.currentState = "AttrSelected";
				attrForm.attrXml = XML(attrGrid.selectedItem);
				attrForm.submitBtnLabel = "Submit";
			}
			
			private function onAttributeChanged(): void {
				var cur: XML = attrForm.attrXml;
				var attrId: int = -1;
				if (this.currentState == "AttrSelected") {
					// Replace the selected attribute with the edited one.
					var old: XML = XML(attrGrid.selectedItem);
					attrId = int(attrGrid.selectedItem.@id);
					old.@name = cur.@name;
					old.@type = cur.@type;
					old.@multi = cur.@multi;
					old.@dup = cur.@dup;
					old.setChildren(cur.children());
				} else {
					// Add the new attribute
					XML(entityTypes.source.(@id==selectedTypeId)[0]).attrDefs.appendChild(cur.copy());
					attrGrid.dataProvider = 
						entityTypes.source.(@id==selectedTypeId)[0].attrDefs.attrDef;
				}
				this.currentState = "TypeSelected";
				attrGrid.selectedIndex = -1;  // Make sure the attribute grid is not selected.
				attrForm.reset();
				attrForm.submitBtnLabel = "Add Attribute";
				
				if (String(cur.@type) == Cst.ATTR_TYPE_ENUM) {
					var enums: Array = new Array();
					for each (var enum: Object in cur.enums.enum) {
						enums.push(XML(enum).text().toString());
					}
					new AddEnumAttributeCommand(String(cur.@name),
						enums, int(selectedTypeId),
						ModelUtil.isTrue(String(cur.@multi)),
						ModelUtil.isTrue(String(cur.@dup)),
						ModelCollection.instance().currentModelId,
						attrId).execute();
				} else if (String(cur.@type) == Cst.ATTR_TYPE_NUMBER) {
					var min: Number = Number(cur.min.text().toString());
					var max: Number = Number(cur.max.text().toString());
					var unit: String = cur.unit.text().toString();
					new AddNumericAttributeCommand(String(cur.@name),
						min, max, unit, int(selectedTypeId),
						ModelUtil.isTrue(String(cur.@multi)),
						ModelUtil.isTrue(String(cur.@dup)),
						ModelCollection.instance().currentModelId,
						attrId).execute();
				} else {
					new AddAttributeCommand(String(cur.@name),
						String(cur.@type), int(selectedTypeId),
						ModelUtil.isTrue(String(cur.@multi)),
						ModelUtil.isTrue(String(cur.@dup)),
						ModelCollection.instance().currentModelId,
						attrId).execute();
				}
				
			}
		
			private function onCancel(): void {
				ClientEvtDispatcher.instance().removeEventListener(
					OperationCommitEvent.EXECUTED_ON_LOCAL, onOperationExecuted);
				PopUpManager.removePopUp(this);
			}
			
			/*private function doCreateModel(): void {
				// Build the model information object for CreateModelCompositeCommand
				var etArray: Array = new Array();
				for each (var top: Object in this.etRoot().et) {
					buildEntypeHierarchy(XML(top), etArray);
				}
				
				var btArray: Array = new Array();
				for each (var binrel: Object in binRelations.source) {
					btArray.push({
						typeName: binrel.@name,
						sourceId: binrel.@left,
						targetId: binrel.@right,
						hier: binrel.@hier,
						dir: binrel.@dir
					});
				}
				
				// Build a refinement relation for each root entity type by default.
				for each (var rootType: Object in this.etRoot().et) {
					btArray.push({
						typeName: rootType.@name + "_" + Cst.BIN_REL_REFINES,
						sourceId: rootType.@id,
						targetId: rootType.@id,
						hier: true,
						dir: true
					});
				}
				
				var info: Object = {
					name: nameInput.text,
						description: introInput.text,
						entypes: etArray,
						bintypes: btArray
				};
				
				new CreateModelCompositeCommand(info).execute();
			}
			
			private function buildEntypeHierarchy(top: XML, dst: Array): void {
				var superId: int = -1;
				var entype: XML = top;
				do {
					dst.push(buildEntype(entype, superId));
					superId = int(entype.@id);
					entype = entype.et[0];
				} while (entype != null);
			}
			
			private function buildEntype(et: XML, superId: int): Object {
				var attrDefs: Array = new Array();
				var defs: XMLList = entityAttrs.source.(@id==String(et.@id));
				if (defs.length() > 0) {
					for each (var def: Object in defs[0].attr) {
						var d: Object = {
							name: def.@name,
								type: def.@type,
								multi: def.@multi,
								dup: def.@dup
						};
						if (Cst.ATTR_TYPE_ENUM == String(def.@type)) {
							var es: Array = new Array();
							for each (var e: Object in def.enums.enum) {
								es.push(XML(e).text().toString());
							}
							d.enums = es;
						} else if (Cst.ATTR_TYPE_NUMBER == String(def.@type)) {
							d.min = def.min.text().toString();
							d.max = def.max.text().toString();
							d.unit = def.unit.text().toString();
						}
						attrDefs.push(d);
					}
				}
				var rslt: Object = {
					id: et.@id,
						typeName: et.@name,
						superId: superId,
						typeId: -1,      // typeId is unknown before creation
						attrDefs: attrDefs
				};
				return rslt;
			} */
		]]>
	</fx:Script>
	
	<s:Label fontSize="12" top="5" left="5" text="The entity types in this model."/>
	<s:HGroup width="100%" height="100%">
		<s:VGroup width="50%" height="100%">
			<s:HGroup>
				<s:Button label="New Type" click="onNewType();"/>
				<s:Button label="New Subtype" 
						  enabled="true" enabled.EntityBegin="false" 
						  click="onNewSubtype();"/>
				<!-- The DELETE operation is not allowed. -->
			</s:HGroup>
			<s:Label fontWeight="bold" text="Current types:"/>
			<mx:Tree id="typeTree" dataProvider="{typeTreeData}"
					 borderStyle="solid" width="100%" height="100%"
					 labelField="@name" itemClick="onTypeClicked(event);"
					 editable="true" showRoot="false" itemEditEnd="onTypeNameChanged(event);"/>
		</s:VGroup>
		
		<s:VGroup width="50%" height="100%" includeIn="AttrSelected, TypeSelected">
			<s:Label fontWeight="bold" text="Attributes: "/>
			
			<mx:DataGrid id="attrGrid" rowCount="6" itemClick="onAttributeClicked();"
						 width="100%">
				<mx:columns>
					<mx:DataGridColumn dataField="@name" headerText="Name" />
					<mx:DataGridColumn dataField="@type" headerText="Type" />
				</mx:columns>
			</mx:DataGrid>
			<s:Label fontWeight="bold" 
					 text.TypeSelected="Add attribute: "
					 text.AttrSelected="Edit attribute: "/>
			<common:AttributeForm id="attrForm" submit="onAttributeChanged();"/>
		</s:VGroup>
	</s:HGroup>
	
</s:TitleWindow>
